def snapshotArtifactory = 'https://s01.oss.sonatype.org/content/repositories/snapshots'
def releaseArtifactory = 'https://s01.oss.sonatype.org/content/repositories/releases'
def mavenName = 'gcdd1993'
afterEvaluate { project ->
    if (project.plugins.hasPlugin("maven-publish")) {
        if (project.plugins.hasPlugin("java")) {
            java {
                withJavadocJar()
                withSourcesJar()
            }
        }
        publishing {
            repositories {
                maven {
                    name "${mavenName}"
                    url = version.endsWith('SNAPSHOT') ? snapshotArtifactory : releaseArtifactory
                    credentials {
                        username "${ossrh_username}"
                        password "${ossrh_password}"
                    }
                    authentication {
                        basic(BasicAuthentication)
                    }
                }
            }
            publications {
                emqxJavaSdk(MavenPublication) {
                    from components.java
                    versionMapping {
                        usage('java-api') {
                            fromResolutionOf('runtimeClasspath')
                        }
                        usage('java-runtime') {
                            fromResolutionResult()
                        }
                    }
                }
            }
        }
    }
}